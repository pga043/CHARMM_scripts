* Molecular Dynamics
* NVT equilibration

if ?openmm .ne. 1 then
   echo "Test NOT performed. OPENMM must be defined in pref.dat"
   stop
endif



bomlev -2

! read topology and parameter files
read rtf card name toppar/top_all36_prot.rtf
read param card flex name toppar/par_all36_prot.prm
stream toppar/toppar_water_ions.str
read rtf card name toppar/top_all36_cgenff.rtf append
read param card flex name toppar/par_all36_cgenff.prm append
stream ligand/pm32_cgenff.str

bomlev 0

! read the psf and coordinate file
read psf card name hne-pm32-neutralized.psf
read coor card name hne-pm32-heat.crd


! SETUP CRYSTAL (DEFINE, BUILD), IMAGE CENTERING W. MODIFIED PSF
! WE USE THE SAME PARAMS AS IN SOLVATION
set greaterval = 67.8527
crystal define cubic @greaterval @greaterval @greaterval 90. 90. 90.
crystal build noper 0

! These segments are used by CHARMMing for water and ions in bulk solvent
define bulks sele segid bwat .or. segid pot .or. segid cl .or. segid sod .or. -
  segid cal .or. segid mg .or. segid ces end

! Set up images -- center the protein by segment and the solvent by residue
image byseg sele .not. resname tip3 .and. .not. bulks end
image byres sele resname tip3 .or. bulks end

! set up shake
shake bonh param sele all end

open unit 41 write card name hne-pm32-equil-1.res !restart file that will be written
open unit 51 read card name hne-pm32-heat.res !restart file that will be reading
open unit 31 write file name hne-pm32-equil-1.dcd !trajectory file name

! set up nonbond parameters -- same as for heating
nbond inbfrq -1 imgfrq -1 atom vatom cdie eps 1.0 -
    elec ewald pmew fftx 48 ffty 48  fftz 48  kappa .34 spline order 6 -
    vdw vswitch cutnb 16.0 cutim 16.0 ctofnb 12. ctonnb 10. wmin 1.0
energy

!!!!!!!!!!!!!!!!!!!LANGEVIN HEATBATH NVT!!!!!!!!!!!!!!!!!!!!!
! Run NVT dynamics with Langevin heatbath, gamma = 10 ps^-1
! Run dynamics in periodic box
dynamics leap restart timestep 0.001 -
     nstep 3000000 nprint 1000 iprfrq 1000 isvfrq 3000000 - ! iprfrq and ntrfrq are related
     iunwri 41 iunrea 51 iuncrd 31 nsavcc 1000 -
     firstt 298 finalt 298  -
     ichecw 0 ihtfrq 0 ieqfrq 0 -
     iasors 1 iasvel 1 iscvel 0  -
     ilbfrq 0 inbfrq -1 imgfrq -1 echeck -1 bycb -
     ntrfrq 1000 - ! To conserve center of mass translation
     omm langevin gamma 10 ! turn on openmm, set-up Langevin


ioform extended

write coor card name hne-pm32-equil.crd
* coordinates after the equilibration run
*


stop

